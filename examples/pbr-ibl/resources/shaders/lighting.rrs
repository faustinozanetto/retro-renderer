#shader vertex
#version 450 core

layout (location = 0) in vec3 a_pos;
layout (location = 1) in vec2 a_tex_coord;

layout (location = 0) out vec2 o_tex_coords;

void main() {
    o_tex_coords = a_tex_coord;
    gl_Position = vec4(a_pos, 1.0f);
}

#shader fragment
#version 450 core

layout (location = 0) out vec4 frag_color;

layout (location = 0) in vec2 o_tex_coord;

layout (binding = 0) uniform sampler2D u_position_map;
layout (binding = 1) uniform sampler2D u_albedo_map;
layout (binding = 2) uniform sampler2D u_normal_map;
layout (binding = 3) uniform sampler2D u_roughmetalao_map;
layout (binding = 4) uniform samplerCube u_irradiance_map;

uniform vec3 u_cam_pos;

struct point_light {
    vec3 position;
    vec3 color;
};

struct spot_light {
    vec3 position;
    vec3 color;
    vec3 direction;

    float inner_angle;
    float outer_angle;
};

uniform point_light p_light;
uniform spot_light s_light;

float light_constant = 1.0f;
float light_linear = 0.09f;
float light_quadratic = 0.032f;

const float PI = 3.14159265359;

float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a = roughness*roughness;
    float a2 = a*a;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;

    float nom   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return nom / denom;
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}

float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2 = GeometrySchlickGGX(NdotV, roughness);
    float ggx1 = GeometrySchlickGGX(NdotL, roughness);

    return ggx1 * ggx2;
}

vec3 FresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

vec3 calculate_point_light(point_light light, vec3 albedo, vec3 normal, vec3 frag_pos, vec3 view_dir, vec3 F0, float roughness, float metallic) {
    vec3 L = normalize(light.position - frag_pos);
    vec3 halway_vec = normalize(view_dir + L);
    float distance = length(light.position - frag_pos);
    float attenuation = 1.0 / (distance * distance);
    vec3 radiance = light.color * 120.0 * attenuation;

      // Cook-Torrance BRDF
    float NDF = DistributionGGX(normal, halway_vec, roughness);   
    float G   = GeometrySmith(normal, view_dir, L, roughness);      
    vec3 F    = FresnelSchlick(max(dot(halway_vec, view_dir), 0.0), F0);

    vec3 numerator    = NDF * G * F; 
    float denominator = 4.0 * max(dot(normal, view_dir), 0.0) * max(dot(normal, L), 0.0) + 0.0001; // + 0.0001 to prevent divide by zero
    vec3 specular = numerator / denominator;

    // kS is equal to Fresnel
    vec3 kS = F;
    // for energy conservation, the diffuse and specular light can't
    // be above 1.0 (unless the surface emits light); to preserve this
    // relationship the diffuse component (kD) should equal 1.0 - kS.
    vec3 kD = vec3(1.0) - kS;
    // multiply kD by the inverse metalness such that only non-metals 
    // have diffuse lighting, or a linear blend if partly metal (pure metals
    // have no diffuse light).
    kD *= 1.0 - metallic;	 

    // scale light by NdotL
    float NdotL = max(dot(normal, L), 0.0);        

    // add to outgoing radiance Lo
    return (kD * albedo / PI + specular) * radiance * NdotL;  
}

vec3 calculate_spot_light(spot_light light, vec3 normal, vec3 frag_pos, vec3 view_dir) { 
    vec3 light_dir = normalize(light.position - frag_pos);
    // diffuse shading
    float diff = max(dot(normal, light_dir), 0.0);
    // attenuation
    float distance = length(light.position - frag_pos);
    float attenuation = 1.0 / (light_constant + light_linear * distance + light_quadratic * (distance * distance));       
    // spotlight intensity
    float theta = dot(light_dir, normalize(-light.direction)); 
    float epsilon = light.inner_angle - light.outer_angle;
    float intensity = clamp((theta - light.outer_angle) / epsilon, 0.0, 1.0);
    // combine results
    vec3 ambient = light.color * vec3(texture(u_albedo_map, o_tex_coord));
    ambient *= attenuation * intensity;
    return ambient;
}

void main()
{
    vec3 frag_pos = texture(u_position_map, o_tex_coord).rgb;
    vec3 albedo = pow(texture(u_albedo_map, o_tex_coord).rgb, vec3(2.2));
    vec3 normal = texture(u_normal_map, o_tex_coord).rgb;
    vec3 roughmetalao = texture(u_roughmetalao_map, o_tex_coord).rgb;
    float roughness = roughmetalao.r;
    float metallic = roughmetalao.g;
    float ambient_occlusion = roughmetalao.b;

    vec3 view_dir = normalize(u_cam_pos - frag_pos);

    vec3 F0 = vec3(0.04); 
    F0 = mix(F0, albedo, metallic);

    vec3 luminance = calculate_point_light(p_light, albedo, normal, frag_pos, view_dir, F0, roughness, metallic);

    // ambient lighting (we now use IBL as the ambient term)
    vec3 kS = FresnelSchlick(max(dot(normal, view_dir), 0.0), F0);
    vec3 kD = 1.0 - kS;
    kD *= 1.0 - metallic;	  
    vec3 irradiance = texture(u_irradiance_map, normal).rgb;
    vec3 diffuse      = irradiance * albedo;
    vec3 ambient = (kD * diffuse) * ambient_occlusion;

    vec3 color = ambient + luminance;
    // HDR tonemapping
    color = color / (color + vec3(1.0));
    // gamma correct
    color = pow(color, vec3(1.0/2.2)); 

    frag_color = vec4(color, 1.0);
}